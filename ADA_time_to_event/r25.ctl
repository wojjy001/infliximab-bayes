$PROBLEM GEERT DATA FIT
$INPUT read=DROP STDY ID IND AMT RATE  DV DVID CMT MDV EVID AGE SEX WT1 BSA CRP1 ALB1 IMMUNO1
 ADA ADAAMT1 DISDUR1 LINE DISACT1 ttfada tlada0 tlast imp ET1 ET2 ET3 ET4 IND1 TIME1 DISDUR IMMUNO DISACT ALB CRP WT ADAAMT TLADADISACT NIND LIND BASET=DROP TIME

;read,STDY,ID,IND,AMT,RATE2,DV,DVID,CMT,MDV,EVID,AGE,SEX,WT,BSA,CRP,ALB,IMMUNO,
;ADA,ADAAMT,DISDUR,PREanti_TNF,DISACT,ttfada,tlada0,tlast,imp,ETA1,ETA2,ETA3,ETA4,ind1,time1,disdur1,immuno1,disact1,alb1,crp1,wt1,adaamt1,tladadisact,nind,lind,baset,newtime




$DATA nmdata2.csv IGNORE=(READ .EQ. #)

$SUBROUTINE ADVAN13 TOL=6

$MODEL
   COMP=(PK1)   ;1
   COMP=(PK2)   ;2
   COMP=(CMHAZ) ;3
   COMP=(HZLAST, INITIALOFF);4
   COMP=(PK1)   ;5
   COMP=(PK2)   ;6
   COMP=(TA1)   ;7
   COMP=(TA2)   ;8

$PK

;SET THE RTLD
IF (NEWIND .LE. 1) BASET=TIME ;INITIALIZE BASET FOR EACH NEW SUBJECT
IF (NEWIND .LE. 1) TSLD=0 ;INITIALIZE TSLD FOR EACH NEW SUBJECT
IF (NEWIND .LE. 1) BASEALB=ALB
IF (BASEALB.LE.0) BASEALB=40

IF (NEWIND .LE. 1) BASEWT=WT
IF (BASEWT.LE.0) BASEWT=70

;NORMALIZE COVARIATES
NWT=(WT/70)
IF (WT .LE. 0) NWT=1
NAGE=(AGE/40)
IF (AGE .LE. 0) NAGE=1
NALB=(ALB/40)
IF (ALB .LE. 0) NALB=1
NCRP=CRP/10
IF (CRP .LE. 0) NCRP=0.1
NDISDUR=DISDUR/15
IF (DISDUR .LE. 0) NDISDUR=1
NTIME=TIME/30

NADAAMT=ADAAMT/100
IF (ADAAMT .LE. 0) NADAAMT=1/100


LINES=EXP(THETA(9)*(LINE-1)) ; LINE STANDARD IS 1


TVCL=THETA(1)
MU_1=LOG(TVCL)
CL=EXP(MU_1+ET1)*NWT**THETA(5)*NALB**THETA(7)*LINES*(1+NADAAMT**THETA(8))

TVV1=THETA(2)
MU_2=LOG(TVV1)
V1=EXP(MU_2+ET2)*NWT**THETA(6)

TVQ=THETA(3)
MU_3=LOG(TVQ)
Q=EXP(MU_3+ET3)*NWT**THETA(5)

TV2=THETA(4)
MU_4=LOG(TV2)
V2=EXP(MU_4+ET4)*NWT**THETA(6)




S1=V1;
;AMT IN MG
;DV=UG/ML WHICH IS MG/L

;TTFADA
B0=THETA(10)+ETA(1)

B1=THETA(11)
ET50=EXP(THETA(12))
ALAG5=128
B2=THETA(13)
B3=THETA(14)

LE=0
IF (LINE.EQ.0) LE=THETA(15)
IF (LINE.EQ.2) LE=THETA(16)
SL=THETA(17)
CP=1/(1+EXP(THETA(18)))

$DES
   ;IF (T.GT.BASET.AND.T.LE.ALAG5) PROP=A(7)/(T-BASET)
   ;IF (T.GT.ALAG5) PROP=(A(7)-A(8))/ALAG5
    IF (T.GT.0) PROP=A(7)/T
   DE=0
   IF (PROP.GT.CP) DE=1
   LH=B0+B1*T/(ET50+T) +B2*(BASEALB-40)+B3*(BASEWT-70)+LE+SL*DE;
   HAZD=EXP(LH)


   ;PK MODEL
   C2=A(1)/V1    ;BLOOD
   C3=A(2)/V2     ;PERIPHERAL
   TA=0
   CV=3
   IF (C2.GT.CV) TA=1
   C5=A(5)/V1
   C6=A(6)/V2
   TA1=0
   IF (C5.GT.CV) TA1=1
   DADT(1)=-CL*C2  -Q*C2 +Q*C3
   DADT(2)=Q*C2 -Q*C3
   DADT(3)=HAZD
   DADT(4)=HAZD
   DADT(5)=-CL*C5  -Q*C5 +Q*C6
   DADT(6)=Q*C5 -Q*C6
   DADT(7)=TA
   DADT(8)=TA1
$ERROR (OBS ONLY)
  TABOVE1=A(7)
  TABOVE2=A(8)
  ;IF (TIME.GT.BASET.AND.TIME.LE.ALAG5)PROP1=TABOVE1/(TIME-BASET)
  ;IF (TIME.GT.ALAG5) PROP1=(TABOVE1-TABOVE2)/ALAG5
  IF (TIME.GT.0) PROP1=A(7)/TIME
  CMHZ=A(3)
  CMHP=A(4)
  C1=A(1)/V1
  DE1=0
  IF (PROP1.GT.CP) DE1=1
  LHAZ=B0+B1*TIME/(ET50+TIME)+B2*(BASEALB-40)+B3*(BASEWT-70)+LE+SL*DE1;+(SLP+SLP2*EXP(-KX*TIME))*C2X

  ST=EXP(-CMHZ)    ;***Survival Function***;
  CMHZD=CMHZ-CMHP  ;Cumulative hazard at dose perior to T2E;
  PT1E=EXP(LHAZ-CMHZ)
  PT2E=EXP(-CMHZD)-EXP(-CMHZ) ;***Probability of T2E***;
  PT3E=EXP(-CMHZ)
  PT4E=1-PT3E
  QE1=0
  QE2=0
  QE3=0
  QE4=0
  IF (DV.EQ.1.AND.TLADA0.GT.0) QE2=1               ;***T2E***;
  IF (DV.EQ.0) QE3=1               ;CENSORED
  IF (DV.EQ.1.AND.TLADA0.LT.0) QE4=1
  LPT1E = LHAZ-CMHZ ;***Log-likelihood of T1E***;
  IF (PT2E.GT.0) THEN
     LPT2E = LOG(PT2E) ;***Log-likelihood of T2E - to avoid LOG(0)***;
  ELSE
     LPT2E =LOG(1)
  ENDIF
  LPT3E = -CMHZ     ;***Log-likelihood of T3E***;
  LPT4E=LOG(PT4E)
  LGL=QE2*LPT2E+QE3*LPT3E+QE4*LPT4E
  Y=-2*LGL




$THETA (4.20E-01  FIX) ;1 CLS
$THETA (4.01E+00  FIX) ;2 V1S
$THETA (3.06E-01  FIX) ;3 QS
$THETA (2.55E+00  FIX) ;4 V2S
$THETA (5.56E-01  FIX) ;5 WT CL
$THETA (6.39E-01 FIX) ;6 WT VDS
$THETA (-1.57E+00  FIX   );7 ALB CL
$THETA (6.58E-01  FIX)   ;8 NADAAMT CL
$THETA (6.38E-02 FIX); 9 LINES ON CL
$THETA (-2.11)            ;10 BASE LN HZ
$THETA (-7.07)            ;11 LINEAR TIME
$THETA (4.62)             ;12 ET50
$THETA (-0.0479)          ;13 ALB
$THETA (-0.0162)          ;14 WT
$THETA (0 FIX)            ;15 LINE 0
$THETA (0 FIX)            ;16 LINE 2
$THETA  (-2.93)          ;17 SL
$THETA  (-2.65)       ;18 CP

$OMEGA 0 FIX



$EST MAX=8000 PRINT=1 METHOD=0 -2LL NOABORT NSIG=2 SIGL=6
$COV COMPRESS MATRIX=R PRINT=E

$TABLE ID TIME BASEWT BASEALB STDY EVID IND1 PROP1 NOPRINT ONEHEADER NOAPPEND FILE=T25.TXT
