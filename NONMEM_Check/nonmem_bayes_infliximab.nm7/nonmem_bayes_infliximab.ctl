$PROBLEM - INFLIXIMAB REFERENCE MODEL
;REFERENCE: 

$INPUT ID TIME AMT RATE WT ADA ALB PPVCL PPVV1 PPVQ PPVV2 DV EVID CMT MDV

$DATA ..\nonmem_bayes_input.csv IGNORE=C

$SUBROUTINE ADVAN6 TOL=5

$MODEL COMP=(CENT,DEFDOS,DEFOBS), COMP=(PERI)

$PK
 ;POPULATION PARAMETERS
 POPCL=THETA(1)
 POPV1=THETA(2)
 POPQ=THETA(3)
 POPV2=THETA(4)

 ;COVARIATE EFFECTS
 WT_CL=THETA(5) ;EFFECT OF WEIGHT ON CLEARANCE
 ALB_CL=THETA(9) ;EFFECT OF ALBUMIN ON CLEARANCE
 ADA_CL=THETA(10) ;EFFECT OF ANTI-DRUG ANTIBODIES ON CLEARANCE
 WT_V1=THETA(6) ;EFFECT OF WEIGHT ON V1
 WT_Q=THETA(7) ;EFFECT OF WEIGHT ON Q
 WT_V2=THETA(8) ;EFFECT OF WEGIHT ON V2

 D1=0.08333333 ;INFUSION INTO THE CENTRAL COMPARTMENT, SPECIFIED AS DURATION=2 HOURS (CONVERTED TO DAYS)
 
 ;ANTI-DRUG ANTIBODIES
 ADACOV=1 ;NO ANTI-DRUG ANTIBODIES
 IF (ADA.EQ.1) ADACOV=1+ADA_CL ;ANTI-DRUG ANTIBODIES PRESENT
 
 ;INDIVIDUAL PARAMETER VALUES
 CL=POPCL*((WT/70)**WT_CL)*((ALB/4)**ALB_CL)*ADACOV*EXP(ETA(1))
 V1=POPV1*((WT/70)**WT_V1)*EXP(ETA(2))
 Q=POPQ*((WT/70)**WT_Q)*EXP(ETA(3))
 V2=POPV2*((WT/70)**WT_V2)*EXP(ETA(4))
 
 S1=V1 ;SCALE FACTOR FOR V

$DES
 DADT(1)=-Q/V1*A(1) +Q/V2*A(2) -CL/V1*A(1) ;CENTRAL COMPARTMENT
 DADT(2)=Q/V1*A(1) -Q/V2*A(2) ;PERIPHERAL COMPARTMENT

$THETA
 (0,0.294,) ;TVCL
 (0.1,3.33,) ;TVV1
 (0,0.0719,) ;TVQ
 (0,1.14,) ;TVV2

 0.614 ;WTONCL #POWER MODEL - REFERENCED TO 70 KG
 0.691 ;WTONV1
 1.10 ;WTONQ
 0.59 ;WTONV2

 -1.17 ;ALBONCL #POWER MODEL - REFERENCED TO 4 G/DL
 0.257 ;ADAONCL #DICHOTOMOUS PROPORTIONAL MODEL - OFFSET IS 1+0.257 WHEN ADA=1

$OMEGA
 0.106929 ;CLBSV  0.327^2
 0.0225 ;V1BSV 0.150^2
 1.21 ;QBSV  1.10^2
 0.638401 ;V2BSV  0.799^2

$SIGMA
 0.175561 ;RUVPROP 0.419^2

$ERROR
 A1=A(1)
 A2=A(2)
 IPRE=F
 Y=F*(1+ERR(1))

;$SIMULATION (1234567) ONLYSIM SUBPROBLEMS=1

$ESTIMATION METHOD=1 INTERACTION MAXEVALS=0 POSTHOC NOABORT NSIG=3 SIGL=9

;$COVARIANCE UNCONDITIONAL SIGL=12 PRINT=E

$TABLE ID TIME AMT RATE A1 A2 CL V1 Q V2 IPRE WT ADA ALB ETA1 PPVCL ETA2 PPVV1 ETA3 PPVQ ETA4 PPVV2 NOPRINT ONEHEADER FILE=nonmem_bayes_infliximab.fit
